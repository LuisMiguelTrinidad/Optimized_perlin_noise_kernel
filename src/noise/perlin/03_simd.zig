// SPDX-License-Identifier: LGPL-3.0-or-later
// Copyright (c) 2026 Luis Miguel Trinidad Salvador
const table = @import("../../core/f32/table.zig");
const vector_math = @import("../../core/f32/vector_math.zig");

const hash = @import("../../core/shared/hash.zig");
const math = @import("../../core/shared/math.zig");

const mode_meta = @import("mode_meta.zig");

const LANES: u8 = @as(u8, @intCast(vector_math.lanes_for(f32)));
const VecF = @Vector(LANES, f32);
const SIMD_ALIGNMENT: u29 = @as(u29, @intCast(vector_math.simd_vector_bytes()));
const VEC_ALIGN: u29 = @alignOf(VecF);

pub const RequiredAlignment: u29 = @max(VEC_ALIGN, SIMD_ALIGNMENT);
pub const sample_kind: mode_meta.SampleKind = .f32;

pub fn workspace_bytes(comptime chunk_size: u64, comptime scale: u64) usize {
	const min_dim: u64 = @min(chunk_size, scale);
	const vec_count: u64 = min_dim / LANES;
	return @as(usize, @intCast(vec_count)) * @sizeOf(VecF) * 3;
}

comptime {
	if (@popCount(@as(u64, LANES)) != 1) {
		@compileError("O3 requires LANES to be a power of two");
	}
	if (@popCount(@as(u64, RequiredAlignment)) != 1) {
		@compileError("RequiredAlignment must be a non-zero power of two");
	}
}

inline fn lerp_vec(a: VecF, b: VecF, t: VecF) VecF {
	const diff: VecF = b - a;
	return (t * diff) + a;
}

inline fn lerp_scalar(a: f32, b: f32, t: f32) f32 {
	const diff: f32 = b - a;
	return (diff * t) + a;
}

inline fn fill_x_cache(
	sx_cache: [*]VecF,
	i_x0_cache: [*]VecF,
	delta_ix_cache: [*]VecF,
	comptime vec_count: usize,
	values_cache_vec: [*]const VecF,
	smooth_cache_vec: [*]const VecF,
	g00x: VecF,
	g10x: VecF,
	g01x: VecF,
	g11x: VecF,
) void {
	const one: VecF = @splat(1.0);

	inline for (0..vec_count) |vec_idx| {
		const x_vec: VecF = values_cache_vec[vec_idx];
		const sx_vec: VecF = smooth_cache_vec[vec_idx];
		const x_m1: VecF = x_vec - one;

		const dx00: VecF = g00x * x_vec;
		const dx10: VecF = g10x * x_m1;
		const dx01: VecF = g01x * x_vec;
		const dx11: VecF = g11x * x_m1;

		const i_x0: VecF = lerp_vec(dx00, dx10, sx_vec);
		const delta_ix: VecF = lerp_vec(dx01 - dx00, dx11 - dx10, sx_vec);

		sx_cache[vec_idx] = sx_vec;
		i_x0_cache[vec_idx] = i_x0;
		delta_ix_cache[vec_idx] = delta_ix;
	}
}

pub inline fn perlin_prepare_cached_values(
	values_buf: []f32,
	smoothed_buf: []f32,
	comptime chunk_size: u64,
	comptime scale: u64,
) void {
	comptime {
		if ((@popCount(scale) != 1)) {
			@compileError("O3 requires SCALE to be a power of two");
		}
		if ((@min(chunk_size, scale) % LANES) != 0) {
			@compileError("O3 requires min(CHUNK_SIZE, SCALE) to be a multiple of LANES");
		}
	}

	for (0..scale) |i| {
		const if32: f32 = @as(f32, @floatFromInt(i)) / @as(f32, @floatFromInt(scale));
		values_buf[i] = if32;
		smoothed_buf[i] = math.smooth_step(if32);
	}
}

pub fn perlin_generate_chunk2d(
	seed: u64,
	comptime chunk_size: u64,
	comptime scale: u64,
	cached_values: []const f32,
	cached_smoothed_values: []const f32,
	world_x: i64,
	world_y: i64,
	noalias out_buffer: []f32,
) !void {
	const lane_mask_u: u64 = @as(u64, LANES) - 1;
	if (((@as(u64, @bitCast(world_x)) & lane_mask_u) != 0) or ((@as(u64, @bitCast(world_y)) & lane_mask_u) != 0)) {
		return error.WorldCoordinatesNotVectorAligned;
	}

	const cs_u: u64 = chunk_size;
	const cs_l: u6 = @as(u6, @intCast(@ctz(cs_u)));

	const sc_u: u64 = scale;
	const sc_m: u64 = sc_u - 1;
	const sc_l: u6 = @as(u6, @intCast(@ctz(sc_u)));

	const arr_world_base_x_i: i64 = world_x;
	const arr_world_base_y_i: i64 = world_y;
	const noise_grid_base_x_i: i64 = arr_world_base_x_i >> sc_l;
	const noise_grid_base_y_i: i64 = arr_world_base_y_i >> sc_l;

	const grids_per_side: u64 = @max(cs_u >> sc_l, @as(u64, 1));
	const gps: usize = @as(usize, @intCast(grids_per_side + 1));

	const arr_world_off_x_u: u64 = @as(u64, @bitCast(arr_world_base_x_i)) & sc_m;
	const arr_world_off_y_u: u64 = @as(u64, @bitCast(arr_world_base_y_i)) & sc_m;

	const min_dim: u64 = comptime @min(chunk_size, scale);
	const vec_count: usize = comptime @as(usize, @intCast(@divExact(min_dim, LANES)));

	const values_base: [*]const f32 = cached_values.ptr + @as(u64, @intCast(arr_world_off_x_u));
	const smooth_base: [*]const f32 = cached_smoothed_values.ptr + @as(u64, @intCast(arr_world_off_x_u));
	const values_cache_vec: [*]const VecF = @as([*]const VecF, @ptrCast(@alignCast(values_base)));
	const smooth_cache_vec: [*]const VecF = @as([*]const VecF, @ptrCast(@alignCast(smooth_base)));

	var sx_cache_arr: [vec_count]VecF = undefined;
	var i_x0_cache_arr: [vec_count]VecF = undefined;
	var delta_ix_cache_arr: [vec_count]VecF = undefined;

	for (0..(gps - 1)) |gy| {
		const noise_local_tile_y_u: u64 = (gps - 1) - gy - 1;
		const arr_world_tile_y_u: u64 = noise_local_tile_y_u << sc_l;

		for (0..(gps - 1)) |gx| {
			const arr_world_tile_x_u: u64 = gx << sc_l;
			const noise_grid_x0_i: i64 = noise_grid_base_x_i + @as(i64, @intCast(gx));
			const noise_grid_y0_i: i64 = noise_grid_base_y_i + @as(i64, @intCast(gy));
			const noise_grid_x1_i: i64 = noise_grid_x0_i + 1;
			const noise_grid_y1_i: i64 = noise_grid_y0_i + 1;

			const g00_idx: u64 = hash.rapidhashnano_no_init(seed, @bitCast(noise_grid_x0_i), @bitCast(noise_grid_y0_i)) & table.grad_mask;
			const g10_idx: u64 = hash.rapidhashnano_no_init(seed, @bitCast(noise_grid_x1_i), @bitCast(noise_grid_y0_i)) & table.grad_mask;
			const g01_idx: u64 = hash.rapidhashnano_no_init(seed, @bitCast(noise_grid_x0_i), @bitCast(noise_grid_y1_i)) & table.grad_mask;
			const g11_idx: u64 = hash.rapidhashnano_no_init(seed, @bitCast(noise_grid_x1_i), @bitCast(noise_grid_y1_i)) & table.grad_mask;

			const g00x: VecF = @splat(table.grad_table[0][g00_idx]);
			const g10x: VecF = @splat(table.grad_table[0][g10_idx]);
			const g01x: VecF = @splat(table.grad_table[0][g01_idx]);
			const g11x: VecF = @splat(table.grad_table[0][g11_idx]);

			const g00y_s: f32 = table.grad_table[1][g00_idx];
			const g10y_s: f32 = table.grad_table[1][g10_idx];
			const g01y_s: f32 = table.grad_table[1][g01_idx];
			const g11y_s: f32 = table.grad_table[1][g11_idx];

			fill_x_cache(
				&sx_cache_arr,
				&i_x0_cache_arr,
				&delta_ix_cache_arr,
				vec_count,
				values_cache_vec,
				smooth_cache_vec,
				g00x,
				g10x,
				g01x,
				g11x,
			);

			var y_idx: u64 = (min_dim - 1 - arr_world_off_y_u) & sc_m;
			var out_row_ptr: [*]f32 = out_buffer.ptr + arr_world_tile_x_u + (arr_world_tile_y_u << cs_l);

			for (0..min_dim) |_| {
				const y_rel: f32 = cached_values[y_idx];
				const sy_ly: f32 = cached_smoothed_values[y_idx];
				const y_rel_m1: f32 = y_rel - 1.0;

				const dy00: f32 = g00y_s * y_rel;
				const dy10: f32 = g10y_s * y_rel;
				const dy01: f32 = g01y_s * y_rel_m1;
				const dy11: f32 = g11y_s * y_rel_m1;

				const y_sy: VecF = @splat(sy_ly);
				const i_y0: VecF = @splat(lerp_scalar(dy00, dy01, sy_ly));
				const delta_iy: VecF = @splat(lerp_scalar(dy10 - dy00, dy11 - dy01, sy_ly));

				const out_row_vec: [*]VecF = @as([*]VecF, @ptrCast(@alignCast(out_row_ptr)));

				inline for (0..vec_count) |vec_idx| {
					const term_x: VecF = i_x0_cache_arr[vec_idx] + (y_sy * delta_ix_cache_arr[vec_idx]);
					const term_y: VecF = i_y0 + (sx_cache_arr[vec_idx] * delta_iy);
					out_row_vec[vec_idx] = term_y + term_x;
				}

				y_idx = (y_idx -% 1) & sc_m;
				out_row_ptr += cs_u;
			}
		}
	}
}
